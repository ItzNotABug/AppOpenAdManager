import com.vanniktech.maven.publish.AndroidSingleVariantLibrary
import com.vanniktech.maven.publish.JavadocJar
import com.vanniktech.maven.publish.SourcesJar

plugins {
    id 'com.android.application' version '9.0.1' apply false
    id 'com.android.library' version '9.0.1' apply false
    id 'org.jetbrains.dokka' version '2.1.0' apply false
    id 'com.vanniktech.maven.publish.base' version '0.36.0' apply false
}

subprojects {
    plugins.withId("com.vanniktech.maven.publish.base") {
        version = "2.6.6"
        group = "com.lazygeniouz"

        mavenPublishing {
            signAllPublications()
            pomFromGradleProperties()
            publishToMavenCentral(true)
            configure(new AndroidSingleVariantLibrary(
                    new JavadocJar.Dokka("dokkaGenerateHtml"),
                    new SourcesJar.Sources(),
                    "release"
            ))
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}